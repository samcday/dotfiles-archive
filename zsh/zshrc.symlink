# all of our zsh files
typeset -U config_files
config_files=($ZSH_DF/**/*.zsh)
# Don't auto-import anything under ohmyzsh-custom. baaaad shit will ensue.
config_files=(${config_files:#$ZSH_DF/ohmyzsh-custom*})
osx_config_files=($ZSH_DF/**/*.zsh.osx)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  sourceFile $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  sourceFile $file
done
case $OSTYPE in
darwin*)
	for file in ${osx_config_files}
	do
		sourceFile $file 
	done
	;;
esac

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  sourceFile ~/.localrc
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  sourceFile $file
done

unset config_files

(( total = $SECONDS - $DOTFILES_START ))
echo "Shell started in ${total}s"

autoload -U promptinit; promptinit
prompt pure
